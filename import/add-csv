#!/home/kevin/.rakudobrew/bin/perl6

use v6;

#use Grammar::Debugger;
use CSV::Parser;

my $file;
my $table;
my @columns;
my %columns;

grammar Description {
  rule TOP { <statement>* }
  rule statement {
             | <file>
             | <table>
             | <columns>
  }
  rule file { 'file' (<identifier>) { $file = ~$0; }}
  rule table {  'table' (<identifier>) { $table = ~$0;} }
  rule columns {
    'columns' '{'
      <column>+
    '}'
  }
  rule column { (<identifier>) (<identifier>) { @columns.push(~$0); %columns{~$0} = $1; } }
  token identifier { (\S+) }
} 

sub MAIN(Str $filename) {
  my $filecontents = $filename.IO.slurp;
  Description.parse($filecontents);

  print qq:to 'HEADER';
    drop table if exists $table;
    create table $table (
    HEADER
  my $eol = '';
  for @columns -> $col {
    my $type = %columns{$col};
    $type ~~ s:g/_/ /;
    print "$eol  $col $type";
    $eol = ",\n";
  }
  print "\n);\n";
  my $fh = open $file, :r or fail "Could not open \"$file\": $!";
  my $parser = CSV::Parser.new( file_handle => $fh, contains_header_row => True );

  my $count = 0;
  my $columns = "(" ~ @columns.join(', ') ~ ')';
  while my %data = %($parser.get_line()) {
    my $delim = '';
    print "INSERT INTO $table $columns VALUES (";
    for @columns -> $col {
      given %columns{$col} {
        when 'integer'           { print $delim,      %data{$col};                           $delim = ',';}
        when 'text'              { print $delim, "'", %data{$col}.subst("'", "''", :g), "'"; $delim = ',';}
        when 'boolean'           { print $delim,      %data{$col} || 'False';                $delim = ',';}
        when 'double_precision'  { print $delim,      %data{$col};                           $delim = ',';}
      }
    }
    print ");\n";
    ++$count;
  }
  note "$count entries created";
}
