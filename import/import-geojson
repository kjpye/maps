#!/usr/bin/env raku

use DB::Pg;

my $dbg = 0;
my $tablename;
my $fd;
my $out;
my $defs;
my $tabledef;
my $dbname = 'maps';
my @columns;
my $columns;
my $line;
my $tabledir = "/tmp/import-tables-$*PID";
my $commandsfile;
my %tables;
my %loadtable;
my $pg; # Database handle
my $dbprefix = '';

sub getline {
    ++$line;
    $fd.get();
}

sub MAIN($file,              #= json database to load
         :$debug   = 0,      #= print debugging information
         :$db      = 'maps', #= database to update
         :$prefix  = '',     #= database table prefix
         :$defonly = 0,      #= list table attributes; don't load data
         :@load,             #= load table
         :@noload,           #= don't load table
        ) {
    $dbname = $db if $db.defined;
    $dbprefix = $prefix;
    $dbg = $debug;
    $defs = $defonly;
note "Reading into database $dbname";
dd $defs;
    # set up output directory
    unless $defs {
        mkdir $tabledir;
        $commandsfile = "$tabledir/commands".IO.open(:w, :append, :0out-buffer);
        $commandsfile.out-buffer = 0;
        # read database table definitions
        note "Updating database $db";
        $pg = DB::Pg.new(conninfo => "dbname=$dbname");
        my $results = $pg.query(
            "SELECT dbtable, dbcolumn, type, sourcecolumn
               FROM defnsw
              WHERE source = 'data'
            ");
        for $results.hashes -> %row {
            my $table  = %row<dbtable>;
            my $column = %row<dbcolumn>;
            my $type   = %row<type>;
            my $source = %row<sourcecolumn>;
            my $target = %row<dbcolumn>;
            %tables{$table}{$source}<type>   = $type;
	    %tables{$table}{$source}<target> = $target;
            %tables{$table}{$target}<source> = $source;
            %loadtable{$table} = 1; # by default, load all tables we know about.
        }
        $pg.finish;
dd %tables;
        if +@load {
            %loadtable = @load Z=> 1 xx *; # overwrites initial value
        }
        if +@noload {
            for @noload { %loadtable{$_} = 0};
        }
        dd %loadtable   if $dbg;
    }
    $fd = $file.IO.open;
    read-json();
}

sub read-json() {
  unless getline() ~~ m:s/ ^ <.ws>? '{' <.ws>? $ / {
    fail "Incorrectly formatted JSON -- doesn't start with '\{'";
    exit 1;
  }
  while read-table() {
    output-table($tablename);
  }
}

sub read-table() {
    note "In read-table" if $dbg;
    my $line = getline();
    if $line ~~ m:s/ ^ <.ws>? '"' (\w+) '"' ':' '{' <.ws>? $ / {
        $tablename = $0;
        note "Table name：$tablename";
        if $defs {
            $out = open "{$tablename}.defs", :w;
        } else {
            $out = open "{$tabledir}/{$dbprefix}{$tablename}.sql", :w;
            dd %tables{$tablename};
            @columns = @();
            for %tables{$tablename}.keys.sort -> $column {
                note "column $column:" if $dbg;
                if %tables{$tablename}{$column}<source>.defined {
                    note "Pushing column $column" if $dbg;
                    @columns.push: $column;
                }
                dd @columns if $dbg;
            }
            $columns = @columns.join: ',';
        }
        read-table-contents();
    } elsif $line ~~ m:s/ ^ <.ws>? '}' ','? $ / {
        return 0;
    } else {
        fail "Could not determine table name";
        exit 2;
    }
    $out.close;
    1;
}

sub output-table($table) {
    dd $table;
    dd $columns;
    my $file = $tabledir ~ '/' ~ $dbprefix ~ $table ~ '.sql';
    my $copycmd = "copy {$dbprefix ~ $table} ({$columns},geometry) from '$file';";
    note $copycmd;
    $commandsfile.put: $copycmd;
    # $pg.query：$copycmd;
}

sub read-table-contents() {
    note "In read-table-contents" if $dbg;
    loop {
        given getline() {
            when m:s/ ^ <.ws>? '}' ','? $ / {
                return;
            }
            when m:s/ ^ <.ws>? '"type"' ':' '"FeatureCollection"' ','? $ / {
                note "type is FeatureCollection" if $dbg;
            }
            when m:s/ ^ <.ws>? '"crs"' ':' '{' $ / {
                read-crs();
            }
            when m:s/ ^ <.ws>? '"features"' ':' '[' $ / {
                read-features();
            }
            default {
                fail "Unknown table field at line $line: $_";
            }
        }
    }
}

sub read-crs() { # Coordinate Reference System
    note "In read-crs" if $dbg;
    loop {
        given getline() {
	    when m:s/ ^ <.ws>? '}' ','? $ / {
                note "Returning from read-crs" if $dbg;
	        return;
            }
            when m:s/ ^ <.ws>? '"type"' ':' '"name"' ','? $ / {
                note "type : name" if $dbg;
            }
            when m:s/ ^ <.ws>? '"properties"' ':' '{' $ / {
                read-crs-properties();
            }
            default {
                fail "Unknown crs field: $_";
            }
        }
    }
}

sub read-crs-properties() {
    note "In read-crs-properties" if $dbg;
    loop {
        given getline() {
	    when m:s/ ^ <.ws>? '}' $ / {
	        note "Returning from read-crs-properties" if $dbg;
                return;
	    }
            when m:s/ ^ '"name"' ':' '"' (\S+) '"' ','? $ / {
	        note "name: $0" if $dbg;
	    }
	}
    }
}

sub read-features() {
    note "In read-features" if $dbg;
    loop {
        given getline() {
            when m:s/ ^ <.ws> ']' $ / {
                note "Returning from read-features" if $dbg;
                return;
            }
            when m:s/ ^ <.ws> '{' $ / {
                read-feature();
            }
            default {
                fail "unknown entry in feature: $_";
            }
        }
    }
}

sub put-field($field, $value is copy) {
    if $field.ends-with('date') && $value.defined && $value {
        if $value ~~ /(\d\d\d\d)(\d\d)(\d\d)(\d\d)(\d\d)(.*)/ {
            $value = "{$0}-{$1}-{$2}T{$3}:{$4}:{$5}";
        } else {
	    note "Unknown date format '$value'";
	    $value = '\N';
        }
    }
    $value //= '\N';
    $out.print: $value ~ "\t";
}

sub read-feature() {
    my $geometry;
    my $properties;
    note "In read-feature" if $dbg;
    loop {
        note "at top of read-feature loop" if $dbg;
        given getline() {
            when m:s/ ^ <.ws>? '}' ','? $ / {
                unless $defs {
                    fail "No geometry found at line $line" unless $geometry;
                    fail "No properties found" unless $properties.defined;
		    for @columns -> $column {
                        put-field($column, $properties{$column});
		    }
                    $out.say: $geometry;
		}
                note "Returning from read-feature" if $dbg;
                return;
            }
            when m:s/ ^ <.ws> '"type"' ':' '"Feature"' ','? $ / {
                note "read-feature got type : Feature" if $dbg;
            }
            when m:s/ ^ <.ws>? '"geometry"' ':' '{' $ / {
                note "Found \"geometry\"" if $dbg;
                $geometry = read-geometry();
		dd $geometry if $dbg;
            }
            when m:s/ ^ <.ws>? '"properties"' ':' '{' $ / {
                $properties = read-feature-properties();
            }
            default {
                fail "Unknown feature entry: $_";
            }
        }
    }
}

sub tohex($buf) {
    my @chars;
    for $buf.list -> $byte {
        @chars.push: sprintf("%02.2X", $byte);
    }
    @chars.join: '';
}

sub geom-to-ewkb($geometry-type, $geometry, :$byte-order = 1, :$srid = 4326) {
    return '' if $defs;
    my %geometry-types = (
        'geometry'           =>  0,
        'point'              =>  1,
        'linestring'         =>  2,
        'polygon'            =>  3,
        'multipoint'         =>  4,
        'multilinestring'    =>  5,
        'multipolygon'       =>  6,
        'geometrycollection' =>  7,
        'circularstring'     =>  8,
        'compoundcurve'      =>  9,
        'curvepolygon'       => 10,
        'multicurve'         => 11,
        'multisurface'       => 12,
        'curve'              => 13,
        'surface'            => 14,
        'polyhedralsurface'  => 15,
        'tin'                => 16,
    );
    my $geomtype = $geometry-type.lc;
    my $m        = $geomtype ~~ s/m$//;
    my $z        = $geomtype ~~ s/z$//;
    $geomtype = %geometry-types{$geomtype} // -1;
    fail "Unsupported geometry type $geometry-type" if $geomtype < 0;
    $geomtype += 1000 if $z;
    $geomtype += 3000 if $m;
    $geomtype += 0x20000000; # SRID present
    my $coordcount = 2;
    $coordcount++ if $z;
    $coordcount++ if $m;

    my $endian = $byte-order ?? (LittleEndian) !! (BigEndian);
    my $buf;
    given $geometry-type.lc {
        when 'multipolygon' {
            $buf = Buf.new($byte-order, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
            $buf.write-uint32(1, $geomtype,  $endian); # geometry type
            $buf.write-uint32(5, $srid,      $endian); # srid
            $buf.write-uint32(9, +$geometry, $endian); # number of segments
            my $offset = 13;
            for $geometry.list -> $polygon {
                $buf.write-uint32($offset + +$polygon * 8 * $coordcount, 0); # preextend buf for this polygon
                $buf.write-uint32($offset, +$polygon, $endian); # number of segments
                $offset += 4;
                for $polygon.list -> $segment {
                    for $segment.list -> $point {
                        $buf.write-num64($offset, $point[0].Num, $endian); $offset += 8;
                        $buf.write-num64($offset, $point[1].Num, $endian); $offset += 8;
                        if $coordcount > 2 {
                            $buf.write-num64($offset, $point[2].Num, $endian); $offset += 8;
                            if $coordcount > 3 {
                                $buf.write-num64($offset, $point[3].Num, $endian); $offset += 8;
                            }
                        }
                    }
                }
            }
        }
        when 'polygon' |
             'multilinestring' {
            $buf = Buf.new($byte-order, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0);
            $buf.write-uint32(1, $geomtype,  $endian); # geometry type
            $buf.write-uint32(5, $srid,      $endian); # srid
            $buf.write-uint32(9, +$geometry, $endian); # number of segments
            my $offset = 13;
            for $geometry.list -> $segment {
                $buf.write-uint32($offset + +$segment * 8 * $coordcount, 0); # preextend buf for this segment
                $buf.write-uint32($offset, +$segment, $endian); # number of points
                $offset += 4;
                for $segment.list -> $point {
                    $buf.write-num64($offset, $point[0].Num, $endian); $offset += 8;
                    $buf.write-num64($offset, $point[1].Num, $endian); $offset += 8;
                    if $coordcount > 2 {
                        $buf.write-num64($offset, $point[2].Num, $endian); $offset += 8;
                        if $coordcount > 3 {
                            $buf.write-num64($offset, $point[3].Num, $endian); $offset += 8;
                        }
                    }
                }
            }
        }
        when 'point' |
            'linestring' {
            $buf = Buf.new($byte-order);
            my $buflength = 5 + 8 * (+$geometry * $coordcount); # position of a 32-bit int at the end of the buffer
            $buf.write-uint32($buflength, 0); # pre-extend the buffer to its final length
            $buf.write-uint32(1, $geomtype,  $endian); # geometry type
            $buf.write-uint32(5, $srid,      $endian); # srid
            $buf.write-uint32(9, +$geometry, $endian); # number of points
            my $offset = 13;
            for $geometry.list -> $point {
                dd $point if $dbg;
                $buf.write-num64($offset, $point[0].Num, $endian); $offset += 8;
                $buf.write-num64($offset, $point[1].Num, $endian); $offset += 8;
                if $coordcount > 2 {
                    $buf.write-num64($offset, $point[2].Num, $endian); $offset += 8;
                    if $coordcount > 3 {
                        $buf.write-num64($offset, $point[3].Num, $endian); $offset += 8;
                    }
                }
            }
        }
        default {
            fail "Unhandled geometry type $geomtype in geom-to-ewkb";
        }
    }
#    $buf.list>>.base(16).join(''); # convert to hex string
    tohex($buf);
}

my token number {
    [ '+' | '-' ]?
    [
        | \d+
	| \d+ '.' \d*
	| '.' \d+
    ]
    [ <[eE]> \d+ ]?
}

sub read-geometry() {
    my $geometry;
    note "In read-geometry" if $dbg;
    my $geometry-type;
    loop {
        note "at top of read-geometry loop" if $dbg;
        given getline() {
            note "Next line is \"$_\"" if $dbg;
            when m:s/ ^ <.ws>? '}' ','? $ / {
                note "Returning from read-geometry" if $dbg;
                return geom-to-ewkb($geometry-type, $geometry);
            }
            when m:s/ ^ <.ws>? '"type"' ':' '"' (\w+) '"' ','? $ / {
                $geometry-type = $0;
                note "Got geometry type {$geometry-type}" if $dbg;
            }
            when m:s/ ^ <.ws>? '"coordinates"' ':' '[' $ / { # multiple points
                $geometry = read-coordinates();
		dd $geometry if $dbg;
                note "Back from read-coordinates" if $dbg;
            }
            when m:s/ ^ <.ws>? '"coordinates"' ':' '['
                    (<number>) ','
                    (<number>) ']' $ / { # single point -- two coordinates
                $geometry = List.new: (~$0, ~$1);
		dd $geometry if $dbg;
            }
            when m:s/ ^ <.ws>? '"coordinates"' ':' '['
                    (<number>) ','
                    (<number>) ','
                    (<number>) ']' $ / { # single point -- three coordinates
                $geometry = List.new: (~$0, ~$1, ~$2);
		dd $geometry if $dbg;
            }
            when m:s/ ^ <.ws>? '"coordinates"' ':' '['
                    (<number>) ','
                    (<number>) ','
                    (<number>) ','
                    (<number>) ']' $ / { # single point -- four coordinates
                $geometry = List.new: (~$0, ~$1, ~$2, ~$3);
		dd $geometry if $dbg;
            }
            default {
                fail "Unknown geometry field: \"$_\"";
            }
        }
    }
}

sub read-coordinates() {
    note "In read-coordinates" if $dbg;
    my @coords;
    loop {
        given getline() {
            when m:s/ ^ <.ws>? ']' ','? $ / {
                dd @coords if $dbg;
                note "Returning from read-coordinates" if $dbg;
		return @coords;
	    }
            when m:s/ <.ws> '[' $ / {
                # nested coordinates
                my $subgeometry = read-coordinates();
                @coords.push: $subgeometry;
            }
            when m:s/ <.ws> '[' (<number>) ',' (<number>) [',' <number>]? ']' ','? $ / {
	        @coords.push: (+$0, +$1);
            }
	}
    }
}

sub read-feature-properties() {
    note "In read-geometry-properties" if $dbg;
    my %properties;
    loop {
        given getline() {
	    when m:s/ ^ <.ws>? '}' $ / {
                dd %properties if $dbg;
                if $defs {
                    for %properties.kv -> $key, $value {
                        $out.say: $key, "\t", $value;
                    }
                }
	        note "Returning from read-feature-properties" if $dbg;
                return %properties;
	    }
            when m:s/ ^ '"' (\S+) '"' ':' '"' (\S+) '"' ','? $ / {
                %properties{~$0} = ~$1;
	        note "name: {~$0}" if $dbg;
	    }
            when m:s/ ^ '"' (\S+) '"' ':' (<number>) ','? $ / {
                %properties{~$0} = ~$1;
	        note "name: {~$0}" if $dbg;
	    }
            when m:s/ ^ '"' (\S+) '"' ':' 'null' ','? $ / {
                %properties{~$0} = Nil;
	        note "name: {~$0}" if $dbg;
	    }
            default {

	    }
	}
    }
}

